plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        applicationId "com.blinkreceipt.directscan"

        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk

        versionCode versions.versionMajor * 10000 + versions.versionMinor * 1000 + versions.versionPatch * 100
        versionName "${versions.versionMajor}.${versions.versionMinor}.${versions.versionPatch}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            //noinspection ChromeOsAbiSupport
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }

    buildFeatures {
        viewBinding true
    }

    signingConfigs {
        release {
            storeFile file("../blink-receipt-demos.jsk")
            storePassword "${keys.storePassword}"
            keyAlias "${keys.keyAlias}"
            keyPassword "${keys.keyPassword}"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    packagingOptions {
        resources {
            excludes += [
                    'META-INF/NOTICE.md',
                    'META-INF/LICENSE.md',
                    'META-INF/ASL2.0',
                    'META-INF/AL2.0',
                    'META-INF/LGPL2.1',
                    'META-INF/licenses/ASM',
                    'META-INF/DEPENDENCIES',
            ]
        }
    }

    namespace 'com.blinkreceipt.directscan'

}

dependencies {
    implementation("com.google.android.material:material:${versions.material}")

    implementation(platform("com.microblink.blinkreceipt:blinkreceipt-bom:${versions.bom}"))
    implementation("com.microblink.blinkreceipt:blinkreceipt-recognizer")

    testImplementation "junit:junit:${versions.junit}"

    androidTestImplementation "androidx.test.ext:junit:${androidx.junit}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${versions.espresso}"
}